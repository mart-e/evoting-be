/* Filename            : RESULBUR.p                                      */
/*
<pvcs>
<workFile>$Workfile:   RESULBUR.P  $</workFile>
<revision>$Revision: 1.4 $</revision>
<workFileTimeStamp>$Modtime:   Mar 09 2004 16:44:30  $</workFileTimeStamp>

<archive>$Archive:   C:/PVCS VM/v6.8.00/DIGIVOTE/Archives/VOTE/TOT/RESULBUR.P-arc  $</archive>
<archiveTimeStamp>$Date: 2005/03/01 16:11:58 $</archiveTimeStamp>
<pvcsUser>$Author: jru $</pvcsUser>
</pvcs>
*/

{chklevel.i 1}

/************************************************************************/
/*                                                                      */
/*        INPUT PARAMETERS                                              */
/*                                                                      */
/************************************************************************/
DEF INPUT PARAM wz-result   AS LOGICAL  FORMAT "FINAL/VOORLOPIG" NO-UNDO.

/************************************************************************/
/*                                                                      */
/*        LOCAL VARIABLES                                               */
/*                                                                      */
/************************************************************************/
DEF VAR votes           AS I NO-UNDO.
DEF VAR blanco          AS I NO-UNDO.
DEF VAR p_vote          AS I NO-UNDO.
DEF VAR wz-afdrloop     AS C NO-UNDO.
DEF VAR wz-afdrvlg      AS I NO-UNDO.

/************************************************************************/
/*                                                                      */
/*        INCLUDE FILES                                                 */
/*                                                                      */
/************************************************************************/
/*@source sec=1*/

/*@source sec=0*/
{typvar.i}
{selvar.i}

{printer.i "SHARED"}       /* Variabelen om printerbestanden te beheren */

/************************************************************************/
/*                                                                      */
/*        START PROCEDURE                                               */
/*                                                                      */
/************************************************************************/
FIND FIRST setup     NO-LOCK NO-ERROR.
FIND FIRST selection NO-LOCK NO-ERROR.
FIND FIRST session   WHERE session.s-id = selection.s-id
                     NO-LOCK NO-ERROR.

/* Pre-fill needed query-fields for 'usage'                             */
ASSIGN wz-doc-type = 9              /* Gedeel/Finale resultaten         */
       wz-setup-id = 0              /* All MAMA-disks                   */
       wz-et-combi = TRUE           /* Only documents for all elections */
       wz-et-ids   = "".            /* Concerning elections             */

/* Afdrukloop bepalen aan de hand van session.lang2 */
wz-afdrloop =
    IF      session.lang2 = 0   THEN "1"   /* Nederlands  */
    ELSE IF session.lang2 = 1   THEN "2"   /* Frans       */
    ELSE IF session.lang2 = 2   THEN "3"   /* Duits       */
    ELSE IF session.lang2 = 3   THEN "1,2" /* Ned. & Fr.  */
    ELSE IF session.lang2 = 4   THEN "2,3" /* Fr. & Duits */
    ELSE "1". /* Om toch een taal te hebben, maar kan normaal niet */

AfdrukLoop:
DO wz-afdrvlg = 1 TO NUM-ENTRIES(wz-afdrloop) : /* Aantal exemplaren */

    /* Taalkode voor afdruk van dit exemplaar bepalen */
    ASSIGN wz-doc-lang = INT(ENTRY(wz-afdrvlg,wz-afdrloop)).

    {call.i &prg="getusage"}
    IF wz-doc-id = -1
    THEN LEAVE AfdrukLoop.

    FIND FIRST layout WHERE layout.doc-id   = wz-doc-id
                        AND layout.sect-nbr = 0
                        AND layout.page-nbr = 0
                        AND layout.line-nbr = 0
                      NO-LOCK NO-ERROR.
    IF AVAILABLE layout
    THEN PagHeader = SUBSTRING(AllSpaces, 1, 75 - LENGTH(layout.contents)) +
                     layout.contents.
    ELSE PagHeader = "".
    {BEGPRT.I "Y" 85 9}

    ASSIGN wz-page-nbr = 0
           wz-line-nbr = 0.
    FOR EACH election NO-LOCK WHERE election.s-id = selection.s-id
                                AND election.e-pr = TRUE:

        FOR EACH layout WHERE layout.doc-id   = wz-doc-id
                          AND layout.sect-nbr = 1
                      NO-LOCK:
            wz-lijn = layout.contents.
            {PAGEBRK.I}
            {LBLCONV.I 11}
            LBLCONV12:
            REPEAT WHILE INDEX(wz-lijn,"@F") > 0 :
                ASSIGN
                wz-convto   = ""
                wz-startpos = INDEX(wz-lijn,"@F")
                wz-label    = SUBSTR(wz-lijn,wz-startpos + 2,2)
                wz-resvpos  = INTEGER(SUBSTR(wz-lijn,wz-startpos + 5,2)).

                ASSIGN
                wz-convto =
                    IF      wz-label = "01" /* Titel */
                    THEN IF wz-result
                         THEN ""               /*   FINALE RESULTATEN     */
                         ELSE ENTRY(wz-doc-lang,"GEDEELTELIJKE RESULTATEN," +
                                                "   RESULTATS PARTIELS   ," +
                                                "     TEILERGEBNISSE     ")
                    ELSE IF wz-label = "02"
                    THEN setup.sys-type +
                         setup.org-type +
                         setup.area     +
                         STRING(setup.orginator,"999")
                    ELSE IF wz-label = "03"
                    THEN election.long-name[wz-doc-lang]
                    ELSE IF wz-label = "50" /* College informatie */
                    THEN STRING(IF election.colls
                                THEN ( IF wz-doc-lang = 2
                                       THEN election.head0[wz-doc-lang] + " " +
                                            election.coll_name[wz-doc-lang]
                                       ELSE election.coll_name[wz-doc-lang] + " " +
                                            election.head0[wz-doc-lang]             )
                                ELSE "","x(45)")
                    ELSE IF wz-label = "51" /* Heading1 informatie */
                    THEN STRING(( IF election.heading1[wz-doc-lang] = ""
                                  OR election.head1[wz-doc-lang]    = ""
                                  THEN ""
                                  ELSE election.head1[wz-doc-lang] + " : " +
                                       election.heading1[wz-doc-lang]),"x(45)")
                    ELSE IF wz-label = "52" /* Heading2 informatie */
                    THEN STRING(( IF election.heading2[wz-doc-lang] = ""
                                  OR election.head2[wz-doc-lang]    = ""
                                  THEN ""
                                  ELSE election.head2[wz-doc-lang] + " : " +
                                       election.heading2[wz-doc-lang]),"x(45)")
                    ELSE IF wz-label = "53" /* Heading3 informatie */
                    THEN STRING(election.head3[wz-doc-lang] + " : " +
                                election.heading3[wz-doc-lang],"x(45)")
                    ELSE "N/A".
                {REPLLBL.I}
            END. /* REPEAT while index - LBLCONV12 */
            {LINEPRT.I 1}
        END. /* FOR EACH layout */

        votes = 0.
        FOR EACH party OF election NO-LOCK:

            IF party.p-id = 0
            THEN NEXT.    /* Skip blanco party */

/*@source sec=1*/
            call CRYPDEC PRPTOT vote_can.
            p_vote = INTEGER(result4).
            call CRYPDEC PRPTOT vote_sup.
            p_vote = p_vote + INTEGER(result4).
            call CRYPDEC PRPTOT vote_cs.
            p_vote = p_vote + INTEGER(result4).
            call CRYPDEC PRPTOT vote_top.
            p_vote = p_vote + INTEGER(result4).
/*@source sec=0*/

            FOR EACH layout WHERE layout.doc-id   = wz-doc-id
                              AND layout.sect-nbr = 2
                          NO-LOCK:
                wz-lijn = layout.contents.
                {LBLCONV.I 21}
                LBLCONV22:
                REPEAT WHILE INDEX(wz-lijn,"@F") > 0 :
                    ASSIGN
                    wz-convto   = ""
                    wz-startpos = INDEX(wz-lijn,"@F")
                    wz-label    = SUBSTR(wz-lijn,wz-startpos + 2,2)
                    wz-resvpos  = INTEGER(SUBSTR(wz-lijn,wz-startpos + 5,2)).

                    ASSIGN
                    wz-convto   =
                        IF      wz-label = "01"
                        THEN STRING(party.p-id,"99") +
                             " "                     +
                             party.party_name
                        ELSE IF wz-label = "02"
                        THEN STRING(p_vote,"Z,ZZZ,ZZ9")
                        ELSE "N/A".
                    {REPLLBL.I}
                END. /* REPEAT while index - LBLCONV22 */

                {LINEPRT.I 0}
            END. /* FOR EACH layout */
            votes = votes + p_vote.
        END. /* each party of election */

/*@source sec=1*/
        {call.i &prg="blanco" &param=",INPUT election.et-id, INPUT -1, OUTPUT blanco"}
/*@source sec=0*/

        FOR EACH layout WHERE layout.doc-id   = wz-doc-id
                          AND layout.sect-nbr = 3
                        NO-LOCK:
            wz-lijn = layout.contents.
            {LBLCONV.I 31}
            LBLCONV32:
            REPEAT WHILE INDEX(wz-lijn,"@F") > 0 :
                ASSIGN
                wz-convto   = ""
                wz-startpos = INDEX(wz-lijn,"@F")
                wz-label    = SUBSTR(wz-lijn,wz-startpos + 2,2)
                wz-resvpos  = INTEGER(SUBSTR(wz-lijn,wz-startpos + 5,2)).

                ASSIGN
                wz-convto   =
                    IF      wz-label = "01"
                    THEN LC(election.head3[wz-doc-lang])
                    ELSE IF wz-label = "60" /* Geldige stemmen        */
                    THEN STRING(votes,"Z,ZZZ,ZZ9")
                    ELSE IF wz-label = "79" /* Blanco stemmen         */
                    THEN STRING(blanco,"Z,ZZZ,ZZ9")
                    ELSE IF wz-label = "80" /* Geregistreerde kaarten */
                    THEN STRING(votes + blanco,"Z,ZZZ,ZZ9")
                    ELSE "N/A".
                {REPLLBL.I}
            END. /* REPEAT while index - LBLCONV32 */

            {LINEPRT.I 1}
        END. /* FOR EACH layout */
       {PAGPRT.I}
    END. /* election */

    {ENDPRT.I}

END. /* wz-afdrloop : Afdrukloop # talen en # exemplaren */

{return.i}
/* eof */